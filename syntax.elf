%%%%% Abstract syntax %%%%%

%%% Types %%%

ty : type.
%name ty A.

% Usually, we write underlined "C" and "D" for computation types.
% However, all variables in Twelf need to be capital letters,
% so "C" is already taken by computations, and "D" is convenient for dirt,
% thus we label ty types by "B".

bool  : ty.                   % the type of booleans
nat   : ty.                   % the type of natural numbers
unit  : ty.                   % the unit type
empty : ty.                   % the empty type
-->   : ty -> ty -> ty.       % the function type
inst  : effect -> ty.         % the type of instances of a given effect
==>   : ty -> ty -> ty.       % the type of handlers
%infix right 50 -->.
%infix right 55 ==>.


%%% Schemes %%%

sch : type.
%name sch S.

plain  : ty -> sch.            % the type of booleans
forall : (ty -> sch) -> sch.   % the type of natural numbers


concrete : sch -> ty -> type.

concrete/plain : concrete (plain A) A.
concrete/forall : {A} concrete (forall S) A'
                    <- concrete (S A) A'.

%%% Terms %%%

expr : type.
comp : type.
op-cases : type.
%name comp C.
%name expr E.
%name op-cases OCs.

% Abbreviations for handler cases

%abbrev val-case = expr -> comp.
%abbrev op-case = expr -> expr -> comp.
%name op-case OC.

tru : expr.                               % true
fls : expr.                               % false
zro : expr.                               % zero
suc : expr -> expr.                       % successor
unt : expr.                               % unit
fun : ty -> (expr -> comp) -> expr.       % function
ins : instance Eff -> expr.               % instance
hnd : ty -> val-case -> op-cases -> expr. % handler

op-cases/nil  : ty -> op-cases.
op-cases/cons : expr -> opsym Eff -> op-case -> op-cases -> op-cases.

cond : expr -> comp -> comp -> comp.   % conditional statement
mtch : expr -> comp -> (expr -> comp) -> comp. % natural number matching
absd : ty -> expr -> comp.          % eliminator for empty type
app  : expr -> expr -> comp.           % application
val  : expr -> comp.                   % injection of values into computations
op   : expr -> opsym Eff -> expr -> (expr -> comp) -> comp. % operation call
let  : comp -> (expr -> comp) -> comp. % let-binding
letr : ty -> ty -> (expr -> expr -> comp) -> (expr -> comp) -> comp. % recursive definition
with : expr -> comp -> comp.           % handling construct
