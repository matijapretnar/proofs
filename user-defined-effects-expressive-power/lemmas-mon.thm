Specification "mon".

Theorem mon/can-get-case : forall Ms Eff As C L A,
    {mon/of-cases Ms Eff As C} -> {mon/valtys/get As L A} ->
    exists M, {mon/get-case Ms L M}.
induction on 1. intros. case H1.
    case H3.
        case H2.
        case H2.
            search.
            apply IH to H6 H9. search.

Define mon/cong : mon/comp -> mon/comp -> prop by
    mon/cong M M' := {mon/step M M'};
    mon/cong CM CM' := exists C (M : mon/value -> mon/comp) M', CM = C M /\ (forall y, mon/cong (M y) (M' y)) /\ C M' = CM';
    mon/cong M1 M3 := exists M2, mon/cong M1 M2 /\ mon/cong M2 M3.

Theorem mon/cong/plain : forall CM CM',
  (exists C M M', CM = C M /\ mon/cong M M' /\ C M' = CM') -> mon/cong CM CM'.
intros. case H1. unfold 2. exists m\ C (m mon/unit), x\ M, x\ M'. search.

Theorem mon/pluggable : forall E, {mon/hoisting E} -> exists ER, {pi x\ mon/plug E (mon/ret x) (ER x)}.
induction on 1. intros. case H1.
    exists x\ mon/ret x. search.
    apply IH to H2. exists x\ (mon/bind (ER x x) X). search.
    apply IH to H2. exists x\ (mon/app (ER x x) X). search.
    apply IH to H2. exists x\ (mon/prj1 (ER x x)). search.
    apply IH to H2. exists x\ (mon/prj2 (ER x x)). search.

Theorem mon/plug/is-function : forall E M EM, {mon/plug E M EM} -> exists E', forall M', {mon/plug E M' (E' M')}.
induction on 1. intros. case H1.
    exists m\ m. intros. search.
    apply IH to H2. exists m\ mon/bind (E' m) N. intros.
    apply H3 with M' = M'. search.
    apply IH to H2. exists m\ mon/app (E' m) V. intros.
    apply H3 with M' = M'. search.
    apply IH to H2. exists m\ mon/prj1 (E' m). intros.
    apply H3 with M' = M'. search.
    apply IH to H2. exists m\ mon/prj2 (E' m). intros.
    apply H3 with M' = M'. search.
    apply IH to H2. exists m\ mon/reify (E' m) T. intros.
    apply H3 with M' = M'. search.

Theorem mon/plug/unique : forall E M EM EM',
    {mon/plug E M EM} -> {mon/plug E M EM'} -> EM = EM'.
induction on 1. intros. case H1.
    case H2. search.
    case H2. apply IH to H3 H4. search.
    case H2. apply IH to H3 H4. search.
    case H2. apply IH to H3 H4. search.
    case H2. apply IH to H3 H4. search.
    case H2. apply IH to H3 H4. search.

Theorem mon/plug/cong : forall E M1 EM1 M2,
    {mon/plug E M1 EM1} -> mon/cong M1 M2 -> exists EM2, {mon/plug E M2 EM2} /\ mon/cong EM1 EM2.
intros.
    apply mon/plug/is-function to H1.
    exists (E' M2).
    apply H3 with M' = M1.
    apply H3 with M' = M2.
    apply mon/plug/unique to H1 H4.
    split.
    search.
    backchain mon/cong/plain. exists E', M1, M2. search.

Theorem mon/hoisting-equal-eff : forall E C1 C2 Eff1 Eff2,
    {mon/hoisting E} -> {mon/of-evctx E Eff1 C1 Eff2 C2} -> Eff1 = Eff2.
induction on 1. intros. Hwf: case H2. case H1.
    case Hwf. search.
    case Hwf. apply IH to H3 H4. search.
    case Hwf. apply IH to H3 H4. search.
    case Hwf. apply IH to H3 H4. search.
    case Hwf. apply IH to H3 H4. search.

Theorem mon/of-get-case : forall Ms L M Eff As C A,
    {mon/get-case Ms L M} -> {mon/of-cases Ms Eff As C} -> {mon/valtys/get As L A} ->
    {pi x\ mon/of-value x A => mon/of-comp (M x) Eff C}.
% Proof
    induction on 1. intros.
    apart_not_reflexive: assert forall N, {apart N N} -> false.
        induction on 1. intros. case H4. apply IH1 to H5.
    case H1.
        case H2. case H4.
            case H3. search.
            case H9. apply apart_not_reflexive to H11.
        case H2. case H4. case H6. case H3.
            apply apart_not_reflexive to H9.
            apply IH to H5 H10 H13. search.

Theorem mon/of-plug : forall L E M EM Eff1 C1 Eff2 C2,
    {mon/of-evctx E Eff1 C1 Eff2 C2} -> {L |- mon/of-comp M Eff1 C1} -> {mon/plug E M EM} -> {L |- mon/of-comp EM Eff2 C2}.
induction on 1. intros. Hwf: case H1. case Hwf.
    case H3. search.
    case H3. apply IH to H4 H2 H6. search.
    case H3. apply IH to H4 H2 H6. search.
    case H3. apply IH to H4 H2 H5. search.
    case H3. apply IH to H4 H2 H5. search.
    case H3. apply IH to H5 H2 H6. search.

Theorem mon/of-unplug : forall E M EM Eff2 C2,
    {mon/plug E M EM} -> {mon/of-comp EM Eff2 C2} -> exists Eff1 C1, {mon/of-comp M Eff1 C1} /\ {mon/of-evctx E Eff1 C1 Eff2 C2}.
induction on 1. intros. case H2 (keep). case H1.
    search.
    case H3. apply IH to H5 H6. case H9 (keep). search.
    case H3. apply IH to H5 H6. case H9 (keep). search.
    case H3. apply IH to H5 H6. case H8 (keep). search.
    case H3. apply IH to H5 H6. case H8 (keep). search.
    case H3. apply IH to H5 H7. case H8 (keep). search.
