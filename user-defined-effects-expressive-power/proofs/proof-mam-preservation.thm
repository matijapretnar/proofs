Specification "mam".

Theorem mam/of-plug : forall E M EM C1 C2,
    {mam/of-evctx E C1 C2} -> {mam/of-comp M C1} -> {mam/plug E M EM} -> {mam/of-comp EM C2}.
induction on 1. intros. case H1.
    case H3. search.
    case H3. apply IH to H5 H2 H7. search.
    case H3. apply IH to H4 H2 H6. search.
    case H3. apply IH to H6 H2 H7. search.
    case H3. apply IH to H6 H2 H7. search.

Theorem mam/of-unplug : forall E M EM C2,
    {mam/plug E M EM} -> {mam/of-comp EM C2} -> exists C1, {mam/of-comp M C1} /\ {mam/of-evctx E C1 C2}.
induction on 1. intros. case H1.
    search.
    case H2. apply IH to H3 H5. search.
    case H2. apply IH to H3 H4. search.
    case H2. apply IH to H3 H6. search.
    case H2. apply IH to H3 H6. search.

Theorem mam/of-get-case : forall Ms L M As C A,
    {mam/get-case Ms L M} -> {mam/of-cases Ms As C} -> {mam/valtys/get As L A} ->
    {pi x\ mam/of-value x A => mam/of-comp (M x) C}.
induction on 1. intros.
assert forall N, {apart N N} -> false.
    induction on 1. intros. case H4. apply IH1 to H5.
case H1.
    case H2.
        case H3.
            search.
            case H7. apply H4 to H9.
    case H2.
        case H3.
            case H5. apply H4 to H9.
            apply IH to H6 H7 H10. search.

Theorem reduce-preservation : forall M M' C, {mam/of-comp M C} -> {mam/reduce M M'} -> {mam/of-comp M' C}.
intros. case H2.
    case H1. case H3. inst H4 with n1 = V1, n2 = V2. cut H7. search.
    case H1. case H4. apply mam/of-get-case to H3 H5 H7. inst H8 with n1 = V. cut H9. search.
    case H1. case H3. search.
    case H1. case H4. inst H5 with n1 = V. cut H8. search.
    case H1. case H3. inst H6 with n1 = V. cut H7. search.
    case H1. case H5. search.
    case H1. case H5. search.

Theorem preservation : forall M M' C, {mam/of-comp M C} -> {mam/step M M'} -> {mam/of-comp M' C}.
intros. case H2.
    apply mam/of-unplug to H3 H1.
    apply reduce-preservation to H6 H4.
    apply mam/of-plug to H7 H8 H5. search.
