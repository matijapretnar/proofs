Specification "mam".

Theorem of/plug : forall E M EM C1 C2,
    {of/evctx E C1 C2} -> {of/comp M C1} -> {plug E M EM} -> {of/comp EM C2}.
induction on 1. intros. case H1.
    case H3. search.
    case H3. apply IH to H5 H2 H7. search.
    case H3. apply IH to H4 H2 H6. search.

Theorem of/unplug : forall E M EM C2,
    {plug E M EM} -> {of/comp EM C2} -> exists C1, {of/comp M C1} /\ {of/evctx E C1 C2}.
induction on 1. intros. case H1.
    search.
    case H2. apply IH to H3 H5. search.
    case H2. apply IH to H3 H4. search.

Theorem of/get-case : forall Ms L M As C A,
    {get-case Ms L M} -> {of/cases Ms As C} -> {valtys/get As L A} ->
    {pi x\ of/value x A => of/comp (M x) C}.
induction on 1. intros.
assert forall N, {apart N N} -> false.
    induction on 1. intros. case H4. apply IH1 to H5.
case H1.
    case H2.
        case H3.
            search.
            case H7. apply H4 to H9.
    case H2.
        case H3.
            case H5. apply H4 to H9.
            apply IH to H6 H7 H10. search.

Theorem reduce-preservation : forall M M' C, {of/comp M C} -> {reduce M M'} -> {of/comp M' C}.
intros. case H2.
    case H1. case H3. inst H4 with n1 = V1, n2 = V2. cut H7. search.
    case H1. case H4. apply of/get-case to H3 H5 H7. inst H8 with n1 = V. cut H9. search.
    case H1. case H3. search.
    case H1. case H4. inst H5 with n1 = V. cut H7. search.
    case H1. case H3. inst H5 with n1 = V. cut H6. search.

Theorem preservation : forall M M' C, {of/comp M C} -> {step M M'} -> {of/comp M' C}.
intros. case H2.
    apply of/unplug to H3 H1.
    apply reduce-preservation to H6 H4.
    apply of/plug to H7 H8 H5. search.
