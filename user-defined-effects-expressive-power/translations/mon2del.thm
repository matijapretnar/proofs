Specification "mon2del".

Define del/cong : del/comp -> del/comp -> prop by
    del/cong M M' := {del/step M M'};
    del/cong CM CM' := exists C (M : del/value -> del/comp) M', CM = C M /\ (forall y, del/cong (M y) (M' y)) /\ C M' = CM';
    del/cong M1 M3 := exists M2, del/cong M1 M2 /\ del/cong M2 M3.

Theorem del/cong/plain : forall CM CM',
  (exists C M M', CM = C M /\ del/cong M M' /\ C M' = CM') -> del/cong CM CM'.
intros. case H1. unfold 2. exists m\ C (m del/unit), x\ M, x\ M'. search.

Define ctx : olist -> prop by
    ctx nil;
    nabla x x', ctx (mon2del/value x x' :: L) := ctx L.

Define mon/fresh : mon/value -> prop,
       del/fresh : del/value -> prop by
    nabla x, mon/fresh x;
    nabla x, del/fresh x.

Theorem ctx-member : forall L F,
    ctx L -> member F L ->
    exists X X', (F = mon2del/value X X') /\ mon/fresh X /\ del/fresh X'.
induction on 2. intros. case H2.
    case H1. exists n1, n2. search.
    case H1. apply IH to H4 H3. exists (X n2 n1), (X' n2 n1). search.

Theorem member-prune : forall L E, nabla (x : mon/value) (x' : del/value),
    member (E x x') L -> exists F, E = x\ x'\ F.
induction on 1. intros. case H1.
    search.
    apply IH to H2. search.

Theorem ctx-unique : forall L X X' X'',
    ctx L -> member (mon2del/value X X') L -> member (mon2del/value X X'') L -> X' = X''.
induction on 1. intros. case H1.
    case H2.
    case H2.
        case H3.
            search.
            apply member-prune to H5.
        case H3.
            apply member-prune to H5.
            apply IH to H4 H5 H6. search.

Theorem mon2del/nat-unique : forall L N N' N'',
    ctx L -> {L |- mon2del/nat N N'} -> {L |- mon2del/nat N N''} -> N' = N''.
induction on 2. intros. case H2.
    case H3. search.
    apply ctx-member to H1 H5. case H4.
    case H3. apply IH to H1 H4 H5. search.
    apply ctx-member to H1 H6. case H5.
    apply ctx-member to H1 H5. case H4.

Theorem mon2del/label-unique : forall L N N' N'',
    ctx L -> {L |- mon2del/label N N'} -> {L |- mon2del/label N N''} -> N' = N''.
intros.
    case H2.
        case H3.
            apply mon2del/nat-unique to H1 H4 H5. search.
            apply ctx-member to H1 H6. case H5.
    apply ctx-member to H1 H5. case H4.

Theorem mon2del/cases-unique : forall L N N' N'',
    ctx L -> {L |- mon2del/label N N'} -> {L |- mon2del/label N N''} -> N' = N''.
intros.
    case H2.
        case H3.
            apply mon2del/nat-unique to H1 H4 H5. search.
            apply ctx-member to H1 H6. case H5.
    apply ctx-member to H1 H5. case H4.

Theorem mon2del-unique :
    (forall L V V' V'', ctx L -> {L |- mon2del/value V V'} -> {L |- mon2del/value V V''} -> V' = V'') /\
    (forall L M M' M'', ctx L -> {L |- mon2del/comp M M'} -> {L |- mon2del/comp M M''} -> M' = M'') /\
    (forall L Ms Ms' Ms'', ctx L -> {L |- mon2del/cases Ms Ms'} -> {L |- mon2del/cases Ms Ms''} -> Ms' = Ms'').
induction on 2 2 2. split.
% value
intros. case H2.
    % unit
    case H3. search.
    apply ctx-member to H1 H5. case H4. case H6.
    % pair
    case H3. apply IH to H1 H4 H6. apply IH to H1 H5 H7. search.
    apply ctx-member to H1 H7. case H6. case H8.
    % inj
    case H3. apply mon2del/label-unique to H1 H4 H6. apply IH to H1 H5 H7. search.
    apply ctx-member to H1 H7. case H6. case H8.
    % thunk
    case H3. apply IH1 to H1 H4 H5. search.
    apply ctx-member to H1 H6. case H5. case H7.
    % backchain
    apply ctx-member to H1 H5. case H4. case H3.
        case H6. case H6. case H6. case H6.
        apply ctx-member to H1 H9. case H8. apply ctx-unique to H1 H5 H9. search.

% comp
intros. case H2.
    % ret
    case H3. apply IH to H1 H4 H5. search.
    apply ctx-member to H1 H6. case H5.
    % fun
    case H3. apply IH1 to _ H4 H5. search.
    apply ctx-member to H1 H6. case H5.
    % split
    case H3. apply IH to H1 H4 H6. apply IH1 to _ H5 H7. search.
    apply ctx-member to H1 H7. case H6.
    % case
    case H3. apply IH to H1 H4 H6. apply IH2 to _ H5 H7. search.
    apply ctx-member to H1 H7. case H6.
    % force
    case H3. apply IH to H1 H4 H5. search.
    apply ctx-member to H1 H6. case H5.
    % bind
    case H3. apply IH1 to H1 H4 H6. apply IH1 to _ H5 H7. search.
    apply ctx-member to H1 H7. case H6.
    % app
    case H3. apply IH1 to H1 H4 H6. apply IH to H1 H5 H7. search.
    apply ctx-member to H1 H7. case H6.
    % reify
    case H3. apply IH1 to H1 H4 H7. apply IH1 to _ H5 H8. apply IH1 to _ H6 H9. search.
    apply ctx-member to H1 H8. case H7.
    % reflect
    case H3. apply IH1 to H1 H4 H5. search.
    apply ctx-member to H1 H6. case H5.
    % backchain
    apply ctx-member to H1 H5. case H4.

% cases
intros. case H2.
    % nil
    case H3. search.
    apply ctx-member to H1 H5. case H4.
    % cons
    case H3. apply IH2 to H1 H4 H7. apply mon2del/label-unique to H1 H5 H8. apply IH1 to _ H6 H9. search.
    apply ctx-member to H1 H8. case H7.
    % backchain
    apply ctx-member to H1 H5. case H4.

Split mon2del-unique as mon2del/value-unique, mon2del/comp-unique.

Theorem mon2del/evctx-unique : forall E E' E'',
    {mon2del/evctx E E'} -> {mon2del/evctx E E''} -> E' = E''.
induction on 1. intros. case H1.
    case H2. search.
    case H2. apply IH to H3 H5. apply mon2del/comp-unique to _ H4 H6. search.
    case H2. apply IH to H3 H5. apply mon2del/value-unique to _ H4 H6. search.
    case H2. apply IH to H3 H6. apply mon2del/comp-unique to _ H4 H7. apply mon2del/comp-unique to _ H5 H8. search.

Theorem mon2del/hoisting : forall E E',
    {mon/hoisting E} -> {mon2del/evctx E E'} -> {del/hoisting E'}.
induction on 1. intros. case H1.
    case H2. search.
    case H2. apply IH to H3 H4. search.
    case H2. apply IH to H3 H4. search.

Theorem mon2del/unplug : forall E M EM EM',
    {mon/plug E M EM} -> {mon2del/comp EM EM'} ->
    exists E' M', {mon2del/evctx E E'} /\ {mon2del/comp M M'} /\ {del/plug E' M' EM'}.
induction on 1. intros. case H1.
    exists del/hole, EM'. search.
    case H2. apply IH to H3 H4. exists (del/evctx/bind E' N'), M'1. search.
    case H2. apply IH to H3 H4. exists (del/evctx/app E' V'), M'1.  search.
    case H2. apply IH to H3 H4. exists (del/evctx/app (del/evctx/reset E' (x\ del/fun nb\ Nu' x)) (del/thunk (del/fun (x\del/fun (k\Nb' x k))))), M'1. search.

Theorem del/pluggable : forall E, {del/hoisting E} -> exists ER, {pi x\ del/plug E (del/ret x) (ER x)}.
induction on 1. intros. case H1.
    exists x\ del/ret x. search.
    apply IH to H2. exists x\ (del/bind (ER x x) X). search.
    apply IH to H2. exists x\ (del/app (ER x x) X). search.

Theorem mon2del/plug : forall L E M EM E' M' EM',
    {mon/plug E M EM} -> {mon2del/evctx E E'} -> {L |- mon2del/comp M M'} ->
    {del/plug E' M' EM'} -> {L |- mon2del/comp EM EM'}.
induction on 2. intros. case H2.
    case H1. case H4. search.
    case H1. case H4. apply IH to H7 H5 H3 H8. search.
    case H1. case H4. apply IH to H7 H5 H3 H8. search.
    case H1. case H4. case H9. apply IH to H8 H5 H3 H10. search.

Theorem mon2del/apart : forall N1 N2 N1' N2',
    {mon/apart N1 N2} -> {mon2del/nat N1 N1'} -> {mon2del/nat N2 N2'} -> {del/apart N1' N2'}.
induction on 1. intros. case H1.
    case H2. case H3. search.
    case H2. case H3. search.
    case H2. case H3. apply IH to H4 H5 H6. search.

Theorem mon2del/label/apart : forall L1 L2 L1' L2',
    {mon/label/apart L1 L2} -> {mon2del/label L1 L1'} -> {mon2del/label L2 L2'} ->
    {del/label/apart L1' L2'}.
intros. case H1. case H2. case H3. apply mon2del/apart to H4 H5 H6. search.

Theorem mon2del/get-case : forall Ms L M Ms' L',
    {mon/get-case Ms L M} -> {mon2del/cases Ms Ms'} -> {mon2del/label L L'} ->
    exists M', {del/get-case Ms' L' M'}.
induction on 1. intros. case H1.
    case H2. apply mon2del/label-unique to _ H3 H5. search.
    case H2.
        apply IH to H5 H6 H3. exists M'1.
        apply mon2del/label/apart to H4 H3 H7. search.

Theorem mon2del/get-case/unique : forall Ms L M Ms' L' M',
    {mon/get-case Ms L M} -> {mon2del/cases Ms Ms'} -> {mon2del/label L L'} ->
    {del/get-case Ms' L' M'} -> {pi x\ pi x'\ mon2del/value x x' => mon2del/comp (M x) (M' x')}.
induction on 1. intros.
assert forall N, {del/apart N N} -> false.
    induction on 1. intros. case H5. apply IH1 to H6.
case H2.
    case H1.
    case H1.
        apply mon2del/label-unique to _ H3 H7.
        case H4.
            search.
            case H9. apply H5 to H11.
        case H4.
            apply mon2del/label/apart to H9 H3 H7. case H11. apply H5 to H12.
            apply IH to H10 H6 H3 H12. search.

Theorem mon2del/reduce : forall M1 M2 M1' M2',
    {mon2del/comp M1 M1'} -> {mon2del/comp M2 M2'} -> 
    {mon/reduce M1 M2} -> del/cong M1' M2'.
induction on 3. intros. case H3.
% split (pair V1 V2) M ~> M V1 V2
    case H1. case H4. inst H5 with n1 = V1, n2 = V2, n3 = V1', n4 = V2'.
    assert {mon2del/comp (M V1 V2) (M' V1' V2')}. cut H8. search.
    apply mon2del/comp-unique to _ H2 H9. search.
% case (inj V) Ms ~> M V
    case H1. case H5.
    apply mon2del/get-case to H4 H6 H7.
    apply mon2del/get-case/unique to H4 H6 H7 H9.
    inst H10 with n1 = V, n2 = V'1.
    cut H11. apply mon2del/comp-unique to _ H2 H12. search.
% force (thunk M) ~> M
    case H1. case H4.
    apply mon2del/comp-unique to _ H2 H5. search.
% bind (ret V) N ~> N V
    case H1. case H4. inst H5 with n1 = V, n2 = V'.
    assert {mon2del/comp (M V) (N' V')}. cut H7. search.
    apply mon2del/comp-unique to _ H2 H8. search.
% app (fun M) V ~> M V
    case H1. case H4. inst H6 with n1 = V, n2 = V'.
    assert {mon2del/comp (M V) (M'1 V')}. cut H7. search.
    apply mon2del/comp-unique to _ H2 H8. search.
% reify (ret V) (mon Nu Nb) ~> Nu V
    case H1. case H4. inst H5 with n1 = V, n2 = V'.
    unfold 3.
    exists (del/app (del/fun (nb\Nu' V'))
             (del/thunk (del/fun (x\del/fun (k\Nb' x k))))).
    split. search.
    assert {mon2del/comp (Nu V) (Nu' V')}. cut H8. search.
    apply mon2del/comp-unique to _ H2 H9. search.
% reify H[reflect M T] T ~> Nb {M} {\x. reify H[ret x] T}
    case H1. apply mon2del/unplug to H4 H7. case H11.
    apply mon2del/hoisting to H5 H10.
    apply del/pluggable to H14. inst H15 with n1 = n2.
    apply mon2del/plug to H6 H10 _ H16 with L = mon2del/value n1 n2 :: nil.
    inst H9 with
        n1 = (mon/thunk N),
        n2 = (mon/thunk (mon/fun (x\mon/reify (ER x) (mon/mon Nu Nb)))),
        n3 = (del/thunk M'2),
        n4 = (del/thunk (del/fun (x\del/app (del/reset (ER1 x x) (x\ del/fun nb\ Nu' x)) (del/thunk (del/fun x\ del/fun k\ Nb' x k))))).
    cut H18.
    apply mon2del/comp-unique to _ H2 H19.
    unfold 3.
    exists (
        del/app
            (
                (k\del/fun
                 (nb\del/app (del/app (del/force nb) (del/thunk M'2))
                       (del/thunk
                          (del/fun (y\del/app (del/app (del/force k) y) nb)))))
                (del/thunk (del/fun (x\ del/reset (ER1 x x) (x\ del/fun nb\ Nu' x))))
            )
            (del/thunk (del/fun (x\del/fun (k\Nb' x k))))
    ).
    split. search.
    unfold 3.
    exists (
         (nb\del/app (del/app (del/force nb) (del/thunk M'2))
               (del/thunk
                  (del/fun
                     (y\del/app
                          (del/app
                             (del/force
                                (del/thunk
                                   (del/fun
                                      (x\del/reset (ER1 x x)
                                           (x1\del/fun (nb1\Nu' x1))))))
                             y)
                          nb))))
          (del/thunk (del/fun (x\del/fun (k\Nb' x k))))
    ).
    split. search.
    unfold 3.
    exists (
       (del/app
          (del/app (del/fun (x\del/fun (k\Nb' x k)))
             (del/thunk M'2))
          (del/thunk
             (del/fun
                (y\del/app
                     (del/app
                        (del/force
                           (del/thunk
                              (del/fun
                                 (x\del/reset (ER1 x x) (x1\del/fun (nb1\Nu' x1))))))
                        y)
                     (del/thunk (del/fun (x\del/fun (k\Nb' x k))))))))
    ).
    split. search.
    unfold 3.
    exists (
   (del/app ((x\del/fun (k\Nb' x k)) (del/thunk M'2))
      (del/thunk
         (del/fun
            (y\del/app
                 (del/app
                    (del/force
                       (del/thunk
                          (del/fun
                             (x\del/reset (ER1 x x) (x1\del/fun (nb1\Nu' x1))))))
                    y)
                 (del/thunk (del/fun (x\del/fun (k\Nb' x k))))))))

    ).
    split. search.
    unfold 3.
    exists
      (k\Nb' (del/thunk M'2) k)
      (del/thunk
         (del/fun
            (y\del/app
                 (del/app
                    (del/force
                       (del/thunk
                          (del/fun
                             (x\del/reset (ER1 x x) (x1\del/fun (nb1\Nu' x1))))))
                    y)
                 (del/thunk (del/fun (x\del/fun (k\Nb' x k))))))).
    split. search.
    unfold 3.
    exists
      (Nb' (del/thunk M'2)
      (del/thunk
         (del/fun
            (y\del/app
                 (del/app
                          (del/fun
                             (x\del/reset (ER1 x x) (x1\del/fun (nb1\Nu' x1))))
                    y)
                 (del/thunk (del/fun (x\del/fun (k\Nb' x k)))))))).
    split.
    backchain del/cong/plain.
        exists
           red\ (Nb' (del/thunk M'2)
              (del/thunk
                 (del/fun
                    (y\del/app
                         (del/app
                            red
                            y)
                         (del/thunk (del/fun (x\del/fun (k\Nb' x k)))))))).
           search.
    unfold 2.
    exists
      red\ Nb' (del/thunk M'2)
      (del/thunk
         (del/fun red)).
    search.

Theorem plug/step : forall E M1 EM1 M2 EM2,
    {del/plug E M1 EM1} -> {del/plug E M2 EM2} -> {del/step M1 M2} -> {del/step EM1 EM2}.
induction on 1. intros. case H1.
    case H2. search.
    case H2. apply IH to H4 H5 H3. case H6. search.
    case H2. apply IH to H4 H5 H3. case H6. search.
    case H2. apply IH to H4 H5 H3. case H6. search.

Theorem plug/context : forall E, {del/is-evctx E} -> exists E', forall M, {del/plug E M (E' M)}.
induction on 1. intros. case H1.
    exists m\ m. intros. search.
    apply IH to H2. exists m\ del/bind (E' m) N. intros.
    assert {del/plug E1 M (E' M)}. apply H3 with M = M. search. search.
    apply IH to H2. exists m\ del/app (E' m) V. intros.
    assert {del/plug E1 M (E' M)}. apply H3 with M = M. search. search.
    apply IH to H2. exists m\ del/reset (E' m) T. intros.
    assert {del/plug E1 M (E' M)}. apply H3 with M = M. search. search.

Theorem plug/unique : forall E M EM EM',
    {del/plug E M EM} -> {del/plug E M EM'} -> EM = EM'.
induction on 1. intros. case H1.
    case H2. search.
    case H2. apply IH to H3 H4. search.
    case H2. apply IH to H3 H4. search.
    case H2. apply IH to H3 H4. search.

Theorem plug/is-evctx : forall E M EM, {del/plug E M EM} -> {del/is-evctx E}.
induction on 1. intros. case H1.
    search.
    apply IH to H2. search.
    apply IH to H2. search.
    apply IH to H2. search.

Theorem plug/cong : forall E M1 EM1 M2 EM2,
    {del/plug E M1 EM1} -> {del/plug E M2 EM2} -> del/cong M1 M2 -> del/cong EM1 EM2.
intros.
    apply plug/context to _ with E = E.
        apply plug/is-evctx to H1. search.
    apply H4 with M = M1.
    apply H4 with M = M2.
    apply plug/unique to H1 H5. apply plug/unique to H2 H6.
    backchain del/cong/plain.
    exists E', M1, M2. search.

Theorem correctness : forall M1 M2 M1' M2',
    {mon2del/comp M1 M1'} -> {mon2del/comp M2 M2'} -> 
    {mon/step M1 M2} -> del/cong M1' M2'.
intros. case H3.
    apply mon2del/unplug to H4 H1.
    apply mon2del/unplug to H6 H2.
    apply mon2del/reduce to H8 H11 H5.
    apply mon2del/evctx-unique to H7 H10.
    apply plug/cong to H9 H12 H13.
    search.
