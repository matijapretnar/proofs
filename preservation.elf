if-then-typd : {E} {K} if-then-op-case D OC OC' OC'' ->
               cof (OC E K) B ->
               cof (OC' E K) B ->
               cof (OC'' E K) B ->
               type.
%mode if-then-typd +E +K +D +OC +OC' -OC''.

- : if-then-typd _ _ if-then-op-case/yes OC _ OC.
- : if-then-typd _ _ if-then-op-case/no _ OC OC.

%worlds (vars) (if-then-typd _ _ _ _ _ _).
%total {} (if-then-typd _ _ _ _ _ _).

%%% Preservation

preservation-pred : {D} eof E nat -> pred E ~> C' -> cof C' (nat ! D) -> type.
%mode preservation-pred +D +E +S -C'.

- : preservation-pred _ eof/zro ~>/pred-zro (cof/val eof/zro).

- : preservation-pred _ (eof/suc E) ~>/pred-suc (cof/val E).

- : preservation-pred D (eof/sub E _) S C'
     <- preservation-pred D E S C'.

%worlds (vars) (preservation-pred _ _ _ _).
%total {E} (preservation-pred _ E _ _).


preservation-app : eof E1 (A --> B) -> eof E2 A -> app E1 E2 ~> C -> cof C B -> type.
%mode preservation-app +E1 +E2 +S -C.

- : preservation-app (eof/fun E1) E2 ~>/app (E1 _ E2).

- : preservation-app (eof/sub E1 (<t/--> A'<A B<B')) E2 S (cof/sub C B<B')
     <- preservation-app E1 (eof/sub E2 A'<A) S C.

%worlds (vars) (preservation-app _ _ _ _).
%total {E1} (preservation-app E1 _ _ _).



instance-expr-in-region : eof (ins I) (inst R) -> in-region I R -> type.
%mode instance-expr-in-region +E -IinR.

- : instance-expr-in-region (eof/ins IinR) IinR.

- : instance-expr-in-region (eof/sub E (<t/inst R<R')) IinR'
    <- instance-expr-in-region E IinR
    <- in-region/<r IinR R<R' IinR'.

%worlds (vars) (instance-expr-in-region _ _).
%total {E} (instance-expr-in-region E _).

covers-rest : covers (dirt/cons _ _ D) D1 D2 -> covers D D1 D2 -> type.
%mode covers-rest +Covers -Covers'.

- : covers-rest (covers/cons-here _ Covers') Covers'.
- : covers-rest (covers/cons-there _ Covers') Covers'.

%worlds (vars) (covers-rest _ _).
%total {} (covers-rest _ _).

big-lemma-handled :
            in-dirt-instance I O Dcov ->
            sig O A1 A2 ->
            eof Ep A1 ->
            eof K (A2 --> B) ->
            get-op-case I O OCs OC ->
            opcof OCs B Dcov ->
            cof (OC Ep K) B ->
            type.
%mode big-lemma-handled +IOinD +O +Ep +K +Get +OCs -OC.

- : big-lemma-handled in-dirt-instance/here O Ep K
        (get-op-case/cons eq-inst-op/yes _ if-then-op-case/yes)
        (opcof/cons I O OC OCs add-if-singleton/one)
        (OC _ Ep _ K).

- : big-lemma-handled (in-dirt-instance/there IOinD) O Ep K
        (get-op-case/cons Eq GetRest ITE)
        (opcof/cons I' O' OC' OCs add-if-singleton/one)
        OC
        <- big-lemma-handled IOinD O Ep K GetRest OCs OC''
        <- if-then-typd Epp Kk ITE (OC' Epp Ep Kk K) OC'' OC.

- : big-lemma-handled IOinD O Ep K
        (get-op-case/cons Eq GetRest ITE)
        (opcof/cons I' O' OC' OCs _)
        OC
        <- big-lemma-handled IOinD O Ep K GetRest OCs OC''
        <- if-then-typd Epp Kk ITE (OC' Epp Ep Kk K) OC'' OC.

%worlds (vars) (big-lemma-handled _ _ _ _ _ _ _).
%total {Get} (big-lemma-handled _ _ _ _ Get _ _).

instance-in-itself : {I} in-region I (region/cons I region/empty) -> type.
%mode instance-in-itself +I -IinI.

- : instance-in-itself I in-region/here.

%worlds (vars) (instance-in-itself _ _).
%total {} (instance-in-itself _ _).

singleton-in-dirt : in-dirt-instance I O D -> in-dirt (region/cons I region/empty) O D -> type.
%mode singleton-in-dirt +IOinD -IOinD'.

- : singleton-in-dirt IOinD (in-dirt/cons IOinD in-dirt/empty).

%worlds (vars) (singleton-in-dirt _ _).
%total {} (singleton-in-dirt _ _).

big-lemma-unhandled :
            in-dirt-instance I O D' ->
            sig O A1 A2 ->
            eof Ep A1 ->
            eof K (A2 --> A' ! D') ->
            get-op-case I O OCs OC ->
            opcof OCs (A' ! D') _ ->
            cof (OC Ep K) (A' ! D') ->
            type.
%mode big-lemma-unhandled +IOinD +O +Ep +K +Get +OCs -OC.

- : big-lemma-unhandled IOinD O Ep K
        get-op-case/nil
        opcof/nil
        (cof/op (eof/ins IinI) O Ep ([_] [x] cof/app K x) IOinD')
    <- instance-in-itself I IinI
    <- singleton-in-dirt IOinD IOinD'.

- : big-lemma-unhandled IOinD O Ep K
        (get-op-case/cons _ GetRest ITE)
        (opcof/cons _ _ OC OtherOCs _)
        OC''
    <- big-lemma-unhandled IOinD O Ep K GetRest OtherOCs OC'
    <- if-then-typd Epp Kk ITE (OC Epp Ep Kk K) OC' OC''.

%worlds (vars) (big-lemma-unhandled _ _ _ _ _ _ _).
%total {Get} (big-lemma-unhandled _ _ _ _ Get _ _).

big-lemma : in-dirt-instance I O D ->
            D <d D' ->
            sig O A1 A2 ->
            eof Ep A1 ->
            eof K (A2 --> A' ! D'') ->
            get-op-case I O OCs OC ->
            opcof OCs (A' ! D'') Dcov ->
            covers D Dcov D'' -> 
            covers D' Dcov D'' ->
            cof (OC Ep K) (A' ! D'') ->
            type.
%mode big-lemma +IOinD +DD' +O +Ep +K +Get +OCs +Covers +Covers' -OC.

- : big-lemma in-dirt-instance/here _ O Ep K Get OCs (covers/cons-here IOinD _) _ OC
    <- big-lemma-handled IOinD O Ep K Get OCs OC.

- : big-lemma in-dirt-instance/here DD' O Ep K Get OCs (covers/cons-there IOinD _) _ OC
    <- big-lemma-unhandled IOinD O Ep K Get OCs OC.

- : big-lemma (in-dirt-instance/there IOinD) (<d/cons _ DD') O Ep K Get OCs Cov Cov' OC
    <- covers-rest Cov Cov- 
    <- big-lemma IOinD DD' O Ep K Get OCs Cov- Cov' OC.

%worlds (vars) (big-lemma _ _ _ _ _ _ _ _ _ _).
%total {IOinD} (big-lemma IOinD _ _ _ _ _ _ _ _ _).

handler-inversion :
    eof (hnd Cv OCs) (A ! D ==> A' ! D') ->
    ({x} eof x A -> cof (Cv x) (A' ! D')) ->
    opcof OCs (A' ! D') Dcov ->
    covers D Dcov D' ->
    type.
%mode handler-inversion +E -Cv -OCs -Covers.

%worlds (vars) (handler-inversion _ _ _ _).
%trustme %total {} (handler-inversion _ _ _ _).


preservation-with-op : cof (op (ins I) O Ep K) B -> get-op-case I O OCs OC -> B <dt B1 -> eof (hnd Cv OCs) (B1 ==> B2) -> cof (OC Ep (fun ([y] with (hnd Cv OCs) (K y)))) B2 -> type.
%mode preservation-with-op +E +Get +B<B1 +C -C'.

- : preservation-with-op (cof/op Ei O Ep K ROinD) Get (<dt/! A<A' D<D') (eof/hnd Cv OCs Covers) OC
     <- in-dirt/<d ROinD D<D' ROinD'
     <- instance-expr-in-region Ei IinR
     <- instance-in-region-in-dirt IinR ROinD' IOinD'
     <- <d/refl _ D'<D'
     <- big-lemma IOinD' D'<D' O Ep (eof/fun [x] [e] cof/with (eof/hnd Cv OCs Covers) ((cof/sub (K x e) (<dt/! A<A' D<D')))) Get OCs Covers Covers OC.

- : preservation-with-op (cof/sub E B<B') Get B'<B'' C B
     <- <dt/trans B<B' B'<B'' B<B''
     <- preservation-with-op E Get B<B'' C B.

- : preservation-with-op E Get B<B1 (eof/sub C (<t/==> B1<B1' L2)) (cof/sub B L2)
     <- <dt/trans B<B1 B1<B1' B<B1'
     <- preservation-with-op E Get B<B1' C B.

%worlds (vars) (preservation-with-op _ _ _ _ _).
%total {E C} (preservation-with-op E _ _ C _).


preservation : cof C A -> C ~> C' -> cof C' A -> type.
preservation-let : cof C1 (A ! D) -> D <d D' -> ({x} eof x A -> cof (C2 x) (A' ! D')) -> let C1 C2 ~> C' -> cof C' (A' ! D') -> type.
preservation-with : cof C B1 -> eof E (B1 ==> B2) -> with E C ~> C' -> cof C' B2 -> type.
%mode preservation +C +S -C'.
%mode preservation-let +C1 +D<D' +C2 +S -C'.
%mode preservation-with +C +E +S -C'.

- : preservation (cof/cond _ C1 _) ~>/cond-tru C1.

- : preservation (cof/cond _ _ C2) ~>/cond-fls C2.

- : preservation (cof/zro? _) ~>/zro?-zro (cof/val eof/tru).

- : preservation (cof/zro? _) ~>/zro?-suc (cof/val eof/fls).

- : preservation (cof/pred E) S C'
     <- preservation-pred _ E S C'.

- : preservation (cof/app E1 E2) S C'
     <- preservation-app E1 E2 S C'.

- : preservation (cof/let C1 C2) S C'
     <- <d/refl _ D<D
     <- preservation-let C1 D<D C2 S C'.

- : preservation (cof/letr C1 C2) S
     (C2 _ (eof/fun ([_] [x] (cof/letr C1 ([_] [f] C1 _ _ f x))))).

- : preservation (cof/with E C) S C'
     <- preservation-with C E S C'.

- : preservation (cof/sub C CC') S (cof/sub C' CC')
     <- preservation C S C'.


- : preservation-let (cof/sub C1 (<dt/! A<A' D<D')) D'<D'' C2 S C'
     <- <d/trans D<D' D'<D'' D<D''
     <- preservation-let C1 D<D'' ([x] [e] (C2 x (eof/sub e A<A'))) S C'.

- : preservation-let (cof/val E) _ C2 ~>/let-val (C2 _ E).

- : preservation-let (cof/op Ei O Ep K ROinD) D<D' C2 ~>/let-op (cof/op Ei O Ep ([_] [y] (cof/let (cof/sub (K _ y) (<dt/! A<A D<D')) C2)) ROinD')
     <- in-dirt/<d ROinD D<D' ROinD'
     <- <t/refl A A<A.

- : preservation-let C1 D<D' C2 (~>/let-step S) (cof/let C1' C2)
     <- <t/refl _ A<A
     <- preservation (cof/sub C1 (<dt/! A<A D<D')) S C1'.


- : preservation-with (cof/sub C B1<B1') E S C'
     <- <dt/refl _ B2<B2
     <- preservation-with C (eof/sub E (<t/==> B1<B1' B2<B2)) S C'.

- : preservation-with (cof/val E) H ~>/with-val (Cv _ E)
     <- handler-inversion H Cv _ _.

- : preservation-with C E (~>/with-op Get) C'
     <- <dt/refl _ B1<B1
     <- preservation-with-op C Get B1<B1 E C'.

- : preservation-with C E (~>/with-step S) (cof/with E C')
     <- preservation C S C'.


%worlds (vars) (preservation _ _ _) (preservation-let _ _ _ _ _) (preservation-with _ _ _ _).
%total {(S Sl Sw) (C Cl Cw)} (preservation C S _) (preservation-let Cl _ _ Sl _) (preservation-with Cw _ Sw _).
