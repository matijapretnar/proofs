instance-expr-in-region : eof (ins I) (inst R) -> in-region I R -> type.
%mode instance-expr-in-region +E -IinR.

- : instance-expr-in-region (eof/ins IinR) IinR.

- : instance-expr-in-region (eof/sub E (<t/inst R<R')) IinR'
    <- instance-expr-in-region E IinR
    <- in-region/<r IinR R<R' IinR'.

%worlds (vars) (instance-expr-in-region _ _).
%total {E} (instance-expr-in-region E _).


covers-rest : covers (dirt/cons _ _ D) D1 D2 -> covers D D1 D2 -> type.
%mode covers-rest +Cov -Cov'.

- : covers-rest (covers/cons-here _ Cov') Cov'.
- : covers-rest (covers/cons-there _ Cov') Cov'.

%worlds (vars) (covers-rest _ _).
%total {} (covers-rest _ _).


preservation-if-then-op-case : {E} {K} if-then-op-case D OC OC' OC''
                                -> cof (OC E K) B -> cof (OC' E K) B
                                -> cof (OC'' E K) B -> type.
%mode preservation-if-then-op-case +E +K +D +C +C' -C''.

- : preservation-if-then-op-case _ _ if-then-op-case/yes C _ C.
- : preservation-if-then-op-case _ _ if-then-op-case/no _ C C.

%worlds (vars) (preservation-if-then-op-case _ _ _ _ _ _).
%total {} (preservation-if-then-op-case _ _ _ _ _ _).

%%% Preservation

preservation-pred : {D} eof E nat -> pred E ~> C' -> cof C' (nat ! D) -> type.
%mode preservation-pred +D +E +S -C'.

- : preservation-pred _ eof/zro ~>/pred-zro (cof/val eof/zro).

- : preservation-pred _ (eof/suc E) ~>/pred-suc (cof/val E).

- : preservation-pred D (eof/sub E _) S C'
     <- preservation-pred D E S C'.

%worlds (vars) (preservation-pred _ _ _ _).
%total {E} (preservation-pred _ E _ _).


preservation-app : eof E1 (A --> B) -> eof E2 A -> app E1 E2 ~> C -> cof C B -> type.
%mode preservation-app +E1 +E2 +S -C.

- : preservation-app (eof/fun E1) E2 ~>/app (E1 _ E2).

- : preservation-app (eof/sub E1 (<t/--> A'<A B<B')) E2 S (cof/sub C B<B')
     <- preservation-app E1 (eof/sub E2 A'<A) S C.

%worlds (vars) (preservation-app _ _ _ _).
%total {E1} (preservation-app E1 _ _ _).


preservation-handled-op : in-dirt-instance I O Dcov -> sig O A1 A2
                           -> eof Ep A1 -> eof K (A2 --> B)
                           -> get-op-case I O OCs OC -> opcof OCs B Dcov
                           -> cof (OC Ep K) B -> type.
%mode preservation-handled-op +IOinDcov +O +Ep +K +OC +OCs -C'.

- : preservation-handled-op in-dirt-instance/here _ Ep K
        (get-op-case/cons eq-inst-op/yes _ if-then-op-case/yes)
        (opcof/cons _ _ OC _ add-if-singleton/one)
        (OC _ Ep _ K).

- : preservation-handled-op (in-dirt-instance/there IOinDcov) O Ep K
        (get-op-case/cons _ OC'' ITE)
        (opcof/cons _ _ OC' OCs add-if-singleton/one)
        C''
        <- preservation-handled-op IOinDcov O Ep K OC'' OCs C'
        <- preservation-if-then-op-case Epp Kk ITE (OC' Epp Ep Kk K) C' C''.

- : preservation-handled-op IOinDcov O Ep K
        (get-op-case/cons _ OC'' ITE)
        (opcof/cons _ _ OC OCs _)
        C''
        <- preservation-handled-op IOinDcov O Ep K OC'' OCs C'
        <- preservation-if-then-op-case Epp Kk ITE (OC Epp Ep Kk K) C' C''.

%worlds (vars) (preservation-handled-op _ _ _ _ _ _ _).
%total {OC} (preservation-handled-op _ _ _ _ OC _ _).


preservation-unhandled-op : in-dirt-instance I O D' -> sig O A1 A2
                             -> eof Ep A1 -> eof K (A2 --> A' ! D')
                             -> get-op-case I O OCs OC -> opcof OCs (A' ! D') _
                             -> cof (OC Ep K) (A' ! D') -> type.
%mode preservation-unhandled-op +IOinD' +O +Ep +K +OC +OCs -C'.

- : preservation-unhandled-op IOinD' O Ep K
        get-op-case/nil
        opcof/nil
        (cof/op (eof/ins in-region/here) O Ep ([_] [x] cof/app K x) (in-dirt/cons IOinD' in-dirt/empty)).

- : preservation-unhandled-op IOinD' O Ep K
        (get-op-case/cons _ OC'' ITE)
        (opcof/cons _ _ OC OCs _)
        C''
    <- preservation-unhandled-op IOinD' O Ep K OC'' OCs C'
    <- preservation-if-then-op-case Epp Kk ITE (OC Epp Ep Kk K) C' C''.

%worlds (vars) (preservation-unhandled-op _ _ _ _ _ _ _).
%total {OC} (preservation-unhandled-op _ _ _ _ OC _ _).


preservation-with-op : in-dirt-instance I O D -> sig O A1 A2
                        -> eof Ep A1 -> eof K (A2 --> A' ! D')
                        -> get-op-case I O OCs OC -> opcof OCs (A' ! D') Dcov
                        -> covers D Dcov D'
                        -> cof (OC Ep K) (A' ! D') -> type.
%mode preservation-with-op +IOinD +O +Ep +K +OC +OCs +Cov -OC.

- : preservation-with-op in-dirt-instance/here O Ep K OC OCs (covers/cons-here IOinDcov _) C'
    <- preservation-handled-op IOinDcov O Ep K OC OCs C'.

- : preservation-with-op in-dirt-instance/here O Ep K OC OCs (covers/cons-there IOinD' _) C'
    <- preservation-unhandled-op IOinD' O Ep K OC OCs C'.

- : preservation-with-op (in-dirt-instance/there IOinD) O Ep K OC OCs Cov C'
    <- covers-rest Cov Cov' 
    <- preservation-with-op IOinD O Ep K OC OCs Cov' C'.

%worlds (vars) (preservation-with-op _ _ _ _ _ _ _ _).
%total {IOinD} (preservation-with-op IOinD _ _ _ _ _ _ _).


preservation : cof C A -> C ~> C' -> cof C' A -> type.
preservation-let : cof C1 (A ! D) -> D <d D' -> ({x} eof x A -> cof (C2 x) (A' ! D')) -> let C1 C2 ~> C' -> cof C' (A' ! D') -> type.
preservation-with : cof C B1 -> B1 <dt B1' -> eof E (B1' ==> B2) -> with E C ~> C' -> cof C' B2 -> type.
%mode preservation +C +S -C'.
%mode preservation-let +C1 +D<D' +C2 +S -C'.
%mode preservation-with +C +B1<B1' +E +S -C'.

- : preservation (cof/cond _ C1 _) ~>/cond-tru C1.

- : preservation (cof/cond _ _ C2) ~>/cond-fls C2.

- : preservation (cof/zro? _) ~>/zro?-zro (cof/val eof/tru).

- : preservation (cof/zro? _) ~>/zro?-suc (cof/val eof/fls).

- : preservation (cof/pred E) S C'
     <- preservation-pred _ E S C'.

- : preservation (cof/app E1 E2) S C'
     <- preservation-app E1 E2 S C'.

- : preservation (cof/let C1 C2) S C'
     <- <d/refl _ D<D
     <- preservation-let C1 D<D C2 S C'.

- : preservation (cof/letr C1 C2) S
     (C2 _ (eof/fun ([_] [x] (cof/letr C1 ([_] [f] C1 _ _ f x))))).

- : preservation (cof/with E C) S C'
     <- <dt/refl _ B1<B1
     <- preservation-with C B1<B1 E S C'.

- : preservation (cof/sub C B<B') S (cof/sub C' B<B')
     <- preservation C S C'.


- : preservation-let (cof/sub C1 (<dt/! A<A' D<D')) D'<D'' C2 S C'
     <- <d/trans D<D' D'<D'' D<D''
     <- preservation-let C1 D<D'' ([x] [e] (C2 x (eof/sub e A<A'))) S C'.

- : preservation-let (cof/val E) _ C2 ~>/let-val (C2 _ E).

- : preservation-let (cof/op Ei O Ep K ROinD) D<D' C2 ~>/let-op (cof/op Ei O Ep ([_] [y] (cof/let (cof/sub (K _ y) (<dt/! A<A D<D')) C2)) ROinD')
     <- in-dirt/<d ROinD D<D' ROinD'
     <- <t/refl A A<A.

- : preservation-let C1 D<D' C2 (~>/let-step S) (cof/let C1' C2)
     <- <t/refl _ A<A
     <- preservation (cof/sub C1 (<dt/! A<A D<D')) S C1'.


- : preservation-with (cof/sub C B1<B1') B1'<B1'' E S C'
     <- <dt/trans B1<B1' B1'<B1'' B1<B1''
     <- preservation-with C B1<B1'' E S C'.

- : preservation-with C B1<B1' (eof/sub E (<t/==> B1'<B1'' B2''<B2')) S (cof/sub C' B2''<B2')
     <- <dt/trans B1<B1' B1'<B1'' B1<B1''
     <- preservation-with C B1<B1'' E S C'.

- : preservation-with (cof/val E) (<dt/! AA' _) (eof/hnd Cv _ _) ~>/with-val (Cv _ (eof/sub E AA')).

- : preservation-with (cof/op Ei O Ep K ROinD) (<dt/! A<A' D<D') (eof/hnd Cv OCs Cov) (~>/with-op OC) C'
     <- instance-expr-in-region Ei IinR
     <- in-dirt/<d ROinD D<D' ROinD'
     <- instance-in-region-in-dirt IinR ROinD' IOinD'
     <- preservation-with-op IOinD' O Ep (eof/fun [x] [e] cof/with (eof/hnd Cv OCs Cov) (cof/sub (K x e) (<dt/! A<A' D<D'))) OC OCs Cov C'.

- : preservation-with C B1<B1' E (~>/with-step S) (cof/with E C')
     <- preservation (cof/sub C B1<B1') S C'.

%worlds (vars) (preservation _ _ _) (preservation-let _ _ _ _ _) (preservation-with _ _ _ _ _).
%total {(S Sl Sw) (C Cl Ew) (C Cl Cw)} (preservation C S _) (preservation-let Cl _ _ Sl _) (preservation-with Cw _ Ew Sw _).
