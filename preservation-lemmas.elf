%%% Subcases of the preservation theorem %%%

% Like in the proof of the progress theorem, there are a couple of cases
% for which we need to perform further induction due to subtyping.

preservation-mtch : eof Sig E nat -> cof Sig C1 B
                     -> ({x} var x nat -> cof Sig (C2 x) B)
                     -> step Sig (mtch E C1 C2) C -> cof Sig C B -> type.
%mode preservation-mtch +E +C1 +C2 +S -C'.

- : preservation-mtch eof/zro C1 _ step/mtch-zro C1.

- : preservation-mtch (eof/suc E) _ C2 step/mtch-suc C2'
     <- subst C2 E C2'.

- : preservation-mtch (eof/sub E _) C1 C2 S C'
     <- preservation-mtch E C1 C2 S C'.

%worlds (topen) (preservation-mtch _ _ _ _ _).
%total {E} (preservation-mtch E _ _ _ _).


preservation-app : eof Sig E1 (A --> B) -> eof Sig E2 A -> step Sig (app E1 E2) C -> cof Sig C B -> type.
%mode preservation-app +E1 +E2 +S -C.

- : preservation-app (eof/fun E1) E2 step/app E1'
     <- subst E1 E2 E1'.

- : preservation-app (eof/sub E1 (<t/--> A'<A B<B')) E2 S (cof/sub C B<B')
     <- preservation-app E1 (eof/sub E2 A'<A) S C.

- : preservation-app (eof/sub E1 <t/refl) E2 S C
     <- preservation-app E1 E2 S C.

%worlds (topen) (preservation-app _ _ _ _).
%total {E1} (preservation-app E1 _ _ _).



%%% Preservation of handled operations %%%

% The most technical part of the proof lies here. We know an operation is
% handled by a handler either if one of the cases is guaranteed to match it,
% or if it appears in the outgoing dirt. We analyze both cases and show that
% types are preserved in both of them.

% Preservation in case an operation is covered by some operation case

preservation-handled-op : in-dirt (I # O) Dcov -> sig Sig O A1 A2
                           -> eof Sig Ep A1 -> eof Sig K (A2 --> B)
                           -> get-op-case (I # O) OCs OC -> opcof Sig OCs B Dcov
                           -> cof Sig (OC Ep K) B -> type.
%mode preservation-handled-op +IOinDcov +O +Ep +K +OC +OCs -C'.

% When the first case matches.
- : preservation-handled-op _ _ Ep K
     get-op-case/here
     (opcof/cons _ _ OC _ _)
     OC'
     <- subst2 OC Ep K OC'.

% When the first case does not match and does not have a singleton region.
% In this case, the covered dirt remains the same, so we continue as before.
- : preservation-handled-op IOinDcov O Ep K
     (get-op-case/there OC _)
     (opcof/cons _ _ _ OCs _)
     C'
     <- preservation-handled-op IOinDcov O Ep K OC OCs C'.

% When the first case does not match but has a singleton region, the covered
% dirt changes. However, since the case did not match, the handled operation
% must be in the remainder of the covered dirt, so we continue as before.
- : preservation-handled-op IOinDcov O Ep K
     (get-op-case/there OC A)
     (opcof/cons R _ _ OCs add-if-singleton/take)
     C'
     <- instance-expr-in-region R IinR
     <- unequal-instance-in-rest IinR IOinDcov A IOinDcov'
     <- preservation-handled-op IOinDcov' O Ep K OC OCs C'.

%worlds (topen) (preservation-handled-op _ _ _ _ _ _ _).
%total {OC} (preservation-handled-op _ _ _ _ OC _ _).


% Preservation in case an operation appears in the outgoing dirt

preservation-unhandled-op : in-dirt (I # O) D' -> sig Sig O A1 A2
                             -> eof Sig Ep A1 -> eof Sig K (A2 --> A' ! D')
                             -> get-op-case (I # O) OCs OC -> opcof Sig OCs (A' ! D') _
                             -> cof Sig (OC Ep K) (A' ! D') -> type.
%mode preservation-unhandled-op +IOinD' +O +Ep +K +OC +OCs -C'.

% When the first case matches.
- : preservation-unhandled-op _ _ Ep K
     get-op-case/here
     (opcof/cons _ _ OC _ _)
     OC'
     <- subst2 OC Ep K OC'.

% When the first case does not match, but there are more cases,
% we continue as before.
- : preservation-unhandled-op IOinD' O Ep K
     (get-op-case/there OC _)
     (opcof/cons _ _ _ OCs _)
     C'
     <- preservation-unhandled-op IOinD' O Ep K OC OCs C'.

% When there are no more cases, we propagate the operation call.
- : preservation-unhandled-op IOinD' O Ep K
     get-op-case/nil
     opcof/nil
     (cof/op (eof/ins in-region/here) O Ep ([_] [x] cof/app K (eof/var x))
      (in-dirt-region/cons IOinD' in-dirt-region/empty)).


%worlds (topen) (preservation-unhandled-op _ _ _ _ _ _ _).
%total {OC} (preservation-unhandled-op _ _ _ _ OC _ _).


% Preservation in any case

preservation-with-op : in-dirt (I # O) D -> sig Sig O A1 A2
                        -> eof Sig Ep A1 -> eof Sig K (A2 --> A' ! D')
                        -> get-op-case (I # O) OCs OC -> opcof Sig OCs (A' ! D') Dcov
                        -> covers D Dcov D'
                        -> cof Sig (OC Ep K) (A' ! D') -> type.
%mode preservation-with-op +IOinD +O +Ep +K +OC +OCs +Cov -OC.

- : preservation-with-op in-dirt/here O Ep K OC OCs (covers/cons-here IOinDcov _) C'
     <- preservation-handled-op IOinDcov O Ep K OC OCs C'.

- : preservation-with-op in-dirt/here O Ep K OC OCs (covers/cons-there IOinD' _) C'
     <- preservation-unhandled-op IOinD' O Ep K OC OCs C'.

- : preservation-with-op (in-dirt/there IOinD) O Ep K OC OCs Cov C'
     <- covers-rest Cov Cov' 
     <- preservation-with-op IOinD O Ep K OC OCs Cov' C'.

%worlds (topen) (preservation-with-op _ _ _ _ _ _ _ _).
%total {IOinD} (preservation-with-op IOinD _ _ _ _ _ _ _).


=t : ty -> ty -> type.
=t/refl : =t A A.
 
strengthen-effsig : ({t:ty} effsig EffSig O (A1 t) (A2 t)) -> effsig EffSig O A1' A2' -> ({t:ty} =t (A1 t) A1') -> ({t:ty} =t (A2 t) A2') -> type.
%mode strengthen-effsig +O -O' -Eq1 -Eq2.
 
- : strengthen-effsig ([_] effsig/here) effsig/here ([_] =t/refl) ([_] =t/refl).
 
- : strengthen-effsig ([t] (effsig/there (O t))) (effsig/there O') Eq1 Eq2
     <- strengthen-effsig O O' Eq1 Eq2.
 
%worlds (topen) (strengthen-effsig _ _ _ _).
%total {O} (strengthen-effsig O _ _ _).

strengthen-eof : ({t:ty} eof Sig E (A1 t)) -> ({t:ty} =t (A1 t) A1') -> eof Sig E A1' -> type.
%mode strengthen-eof +E +Eq -E'.

- : strengthen-eof E  _ (E bool).

%worlds (topen) (strengthen-eof _ _ _).
%total {} (strengthen-eof _ _ _).

strengthen : ({t:ty} sig Sig O (A1 t) (A2 t)) -> sig Sig O A1' A2' -> ({t:ty} =t (A1 t) A1') -> ({t:ty} =t (A2 t) A2') -> type.
%mode strengthen +O -O' -Eq1 -Eq2.
 
- : strengthen ([t] (sig/here (Eff t))) (sig/here Eff') Eq1 Eq2
     <- strengthen-effsig Eff Eff' Eq1 Eq2.
 
- : strengthen ([t] (sig/there (O t))) (sig/there O') Eq1 Eq2
     <- strengthen O O' Eq1 Eq2.
 
%worlds (topen) (strengthen _ _ _ _).
%total {O} (strengthen O _ _ _).
 
matijas-lemma3 : cof Sig (op _ O _ _) _ -> sig Sig O A1 A2 -> type.
%mode matijas-lemma3 +C -O.
 
- : matijas-lemma3 (cof/op _ O _ _ _) O.

- : matijas-lemma3 (cof/sub C _) O
     <- matijas-lemma3 C O.
 
%worlds (topen) (matijas-lemma3 _ _).
%total {S} (matijas-lemma3 S _).

matijas-lemma2 : scof Sig (op _ O _ _) _ _ -> sig Sig O A1 A2 -> type.
%mode matijas-lemma2 +C -O.
 
- : matijas-lemma2 (scof/plain C) O
     <- matijas-lemma3 C O.
 
- : matijas-lemma2 (scof/forall S) O'
     <- ({t} matijas-lemma2 (S t) (O t))
     <- strengthen O O' _ _.
 
%worlds (topen) (matijas-lemma2 _ _).
%total {S} (matijas-lemma2 S _).

matijas-lemma : cof Sig (op Ei O Ep K) (A ! D) -> sig Sig O A1 A2 -> eof Sig Ei (inst R) -> eof Sig Ep A1 -> ({x} var x A2 -> cof Sig (K x) (A ! D)) -> in-dirt-region R O D -> type.
%mode matijas-lemma +OpCall +O -Ei -Ep -K -ROinD.

- : matijas-lemma (cof/sub OpCall (<dt/! A1<A2 D<D')) O Ei Ep ([x] [dx] (cof/sub (K x dx) (<dt/! A1<A2 D<D'))) ROinD'
    <- matijas-lemma OpCall O Ei Ep K ROinD
    <- in-dirt-region/<d ROinD D<D' ROinD'.

- : matijas-lemma (cof/op Ei O Ep K ROinD) _ Ei Ep K ROinD.

%worlds (topen) (matijas-lemma _ _ _ _ _ _).
%total {C} (matijas-lemma C _ _ _ _ _).


ohads-lemma : scof Sig (op Ei O Ep K) S D ->
              sig Sig O A1 A2 ->
              eof Sig Ei (inst R) ->
              eof Sig Ep A1 -> 
              ({x} var x A2 -> scof Sig (K x) S D) ->
              in-dirt-region R O D -> type.
%mode ohads-lemma +C +O -Ei -Ep -K -ROinD.
 
- : ohads-lemma (scof/forall S) O (Ei bool) (Ep bool) ([x] [dx] (scof/forall ([t] K' x dx t))) ROinD
     <- ({t} ohads-lemma (S t) O (Ei t) (Ep t) ([x] [dx] (K' x dx t)) ROinD).
 
- : ohads-lemma (scof/plain OpCall) O Ei Ep ([x] [dx] scof/plain (K x dx)) ROinD
     <- matijas-lemma OpCall O Ei Ep K ROinD.
 
%worlds (topen) (ohads-lemma _ _ _ _ _ _).
%total {S} (ohads-lemma S _ _ _ _ _).
