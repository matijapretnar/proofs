% We need to use contradiction in the following lemma.

apart-not-refl : apart O O -> {O'} {D} in-dirt O' D -> type.
%mode apart-not-refl +O!O +O' +D -OinD.

- : apart-not-refl (apart/ss O!O) O' D OinD
     <- apart-not-refl O!O O' D OinD.

%worlds (topen) (apart-not-refl _ _ _ _).
%total {O!O} (apart-not-refl O!O _ _ _).


unequal-instance-in-rest : in-dirt O (dirt/cons O' D)
                            -> apart O O' -> in-dirt O D -> type.
%mode unequal-instance-in-rest +OinO'D +O!O' -OinD.

- : unequal-instance-in-rest (in-dirt/there OinD) _ OinD.
- : unequal-instance-in-rest in-dirt/here O!O OinD
     <- apart-not-refl O!O _ _ OinD.
- : unequal-instance-in-rest in-dirt/here _ in-dirt/here.

%worlds () (unequal-instance-in-rest _ _ _).
%total {} (unequal-instance-in-rest _ _ _).



%%% Substitution lemma %%%

% Because we represent variable judgments by "var" instead of "eof",
% we cannot use Twelf substitution, but need to prove an explicit substitution lemma.
% To make the proof go through, we need to add an additional subtyping assumption.

val-ceof : scof Sig (val E) S _ -> seof Sig E S -> type.
%mode val-ceof +V -E.

- : val-ceof (scof/plain (cof/val E)) (seof/plain E).

- : val-ceof (scof/forall S) (seof/forall S')
     <- {t} val-ceof (S t) (S' t).

%worlds (topen) (val-ceof _ _).
%total {V} (val-ceof V _).

concrete-seof : seof Sig E S -> concrete S A -> eof Sig E A -> type.
%mode concrete-seof +S +SA -A.

- : concrete-seof (seof/plain A) _ A.

- : concrete-seof (seof/forall S) (concrete/forall A SA) E
     <- concrete-seof (S A) SA E.

%worlds (open) (concrete-seof _ _ _).
%total {S} (concrete-seof S _ _).


psubst-eof   : seof Sig E' S -> ({x} pvar x S -> eof Sig (E x) A) -> eof Sig (E E') A -> type.
psubst-cof   : seof Sig E S -> ({x} pvar x S -> cof Sig (C x) B) -> cof Sig (C E) B -> type.
psubst-scof   : seof Sig E S -> ({x} pvar x S -> scof Sig (C x) S' D) -> scof Sig (C E) S' D -> type.
psubst-opcof : seof Sig E S -> ({x} pvar x S -> opcof Sig (OCs x) B Dcov)
               -> opcof Sig (OCs E) B Dcov -> type.
%mode psubst-eof +X +E -E'.
%mode psubst-cof +X +C -C'.
%mode psubst-scof +X +C -C'.
%mode psubst-opcof +X +OCs -OCs'.

- : psubst-eof _ ([x] [dx] (eof/var X)) (eof/var X).

- : psubst-eof _ ([x] [dx] (eof/pvar Y SA)) (eof/pvar Y SA).

- : psubst-eof X ([x] [dx] (eof/pvar dx SA)) E
     <- concrete-seof X SA E.

- : psubst-eof _ ([x] [dx] eof/tru) eof/tru.

- : psubst-eof _ ([x] [dx] eof/fls) eof/fls.

- : psubst-eof _ ([x] [dx] eof/zro) eof/zro.

- : psubst-eof X ([x] [dx] eof/suc (E x dx)) (eof/suc E')
     <- psubst-eof X E E'.

- : psubst-eof _ ([x] [dx] eof/unt) eof/unt.

- : psubst-eof X ([x] [dx] eof/fun (F x dx)) (eof/fun F')
     <- {y} {dy} psubst-cof X ([x] [dx] F x dx y dy) (F' y dy).

- : psubst-eof X ([x] [dx] eof/hnd (Cv x dx) (OCs x dx) Cov) (eof/hnd Cv' OCs' Cov)
     <- ({y} {dy} psubst-cof X ([x] [dx] Cv x dx y dy) (Cv' y dy))
     <- psubst-opcof X OCs OCs'.


- : psubst-cof X ([x] [dx] cof/cond (E x dx) (C1 x dx) (C2 x dx)) (cof/cond E' C1' C2')
     <- psubst-eof X E E'
     <- psubst-cof X C1 C1'
     <- psubst-cof X C2 C2'.

- : psubst-cof X ([x] [dx] cof/mtch (E x dx) (C1 x dx) ([y] [dy] C2 x dx y dy)) (cof/mtch E' C1' C2')
     <- psubst-eof X E E'
     <- psubst-cof X C1 C1'
     <- {y} {dy} psubst-cof X ([x] [dx] C2 x dx y dy) (C2' y dy).

- : psubst-cof X ([x] [dx] cof/absd (E x dx)) (cof/absd E')
     <- psubst-eof X E E'.

- : psubst-cof X ([x] [dx] cof/app (E1 x dx) (E2 x dx)) (cof/app E1' E2')
     <- psubst-eof X E1 E1'
     <- psubst-eof X E2 E2'.

- : psubst-cof X ([x] [dx] cof/val (E x dx)) (cof/val E')
     <- psubst-eof X E E'.

- : psubst-cof X ([x] [dx] cof/op O (E x dx) ([y] [dy] K x dx y dy) IOinD)
     (cof/op O E' K' IOinD)
     <- psubst-eof X E E'
     <- {y} {dy} psubst-cof X ([x] [dx] K x dx y dy) (K' y dy).

- : psubst-cof X ([x] [dx] cof/let (C1 x dx) ([y] [dy] C2 x dx y dy)) (cof/let C1' C2')
     <- psubst-scof X C1 C1'
     <- {y} {dy} psubst-cof X ([x] [dx] C2 x dx y dy) (C2' y dy).

- : psubst-cof X
     ([x] [dx] cof/letr ([f : expr] [df : var f _] [y] [dy] C1 x dx f df y dy) ([f] [df] C2 x dx f df))
     (cof/letr C1' C2')
     <- ({y} {dy} {f : expr} {df : var f _} psubst-cof X ([x] [dx] C1 x dx y dy f df) (C1' y dy f df))
     <- ({y} {dy} psubst-cof X ([x] [dx] C2 x dx y dy) (C2' y dy)).

- : psubst-cof X ([x] [dx] cof/with (E x dx) (C x dx)) (cof/with E' C')
     <- psubst-eof X E E'
     <- psubst-cof X C C'.

- : psubst-scof X ([x] [dx] scof/plain (C x dx)) (scof/plain C')
     <- psubst-cof X C C'.

- : psubst-scof X ([x] [dx] scof/forall ([t] C x dx t)) (scof/forall C')
     <- {t} psubst-scof X ([x] [dx] C x dx t) (C' t).

- : psubst-opcof _ ([x] [dx] opcof/nil) opcof/nil.

- : psubst-opcof X ([x] [dx] opcof/cons O ([y] [dy] [k] [dk] K x dx y dy k dk) (OCs x dx))
     (opcof/cons O K' OCs')
     <- ({y} {dy} {k} {dk} psubst-cof X ([x] [dx] K x dx y dy k dk) (K' y dy k dk))
     <- psubst-opcof X OCs OCs'.

%worlds (open) (psubst-eof _ _ _) (psubst-cof _ _ _) (psubst-scof _ _ _) (psubst-opcof _ _ _).
%total {(E C C' C'')} (psubst-eof _ E _) (psubst-cof _ C _) (psubst-scof _ C' _) (psubst-opcof _ C'' _).


subst-eof   : eof Sig E' A -> (var E' A -> eof Sig E A') -> eof Sig E A' -> type.
subst-cof   : eof Sig E' A -> (var E' A -> cof Sig C B) -> cof Sig C B -> type.
subst-scof   : eof Sig E' A -> (var E' A -> scof Sig C S D) -> scof Sig C S D -> type.
subst-opcof : eof Sig E' A -> (var E' A -> opcof Sig OCs B Dcov) -> opcof Sig OCs B Dcov -> type.
%mode subst-eof +X +E -E'.
%mode subst-cof +X +C -C'.
%mode subst-scof +X +C -C'.
%mode subst-opcof +X +OCs -OCs'.

- : subst-eof X ([dx] (eof/var dx)) X.

- : subst-eof _ ([dx] (eof/var Y )) (eof/var Y).

- : subst-eof _ ([dx] (eof/pvar Y SA)) (eof/pvar Y SA).

- : subst-eof _ ([dx] eof/tru) eof/tru.

- : subst-eof _ ([dx] eof/fls) eof/fls.

- : subst-eof _ ([dx] eof/zro) eof/zro.

- : subst-eof X ([dx] eof/suc (E dx)) (eof/suc E')
     <- subst-eof X E E'.

- : subst-eof _ ([dx] eof/unt) eof/unt.

- : subst-eof X ([dx] eof/fun (F dx)) (eof/fun F')
     <- {y} {dy} subst-cof X ([dx] F dx y dy) (F' y dy).

- : subst-eof X ([dx] eof/hnd (Cv dx) (OCs dx) Cov) (eof/hnd Cv' OCs' Cov)
     <- ({y} {dy} subst-cof X ([dx] Cv dx y dy) (Cv' y dy))
     <- subst-opcof X OCs OCs'.


- : subst-cof X ([dx] cof/cond (E dx) (C1 dx) (C2 dx)) (cof/cond E' C1' C2')
     <- subst-eof X E E'
     <- subst-cof X C1 C1'
     <- subst-cof X C2 C2'.

- : subst-cof X ([dx] cof/mtch (E dx) (C1 dx) ([y] [dy] C2 dx y dy)) (cof/mtch E' C1' C2')
     <- subst-eof X E E'
     <- subst-cof X C1 C1'
     <- {y} {dy} subst-cof X ([dx] C2 dx y dy) (C2' y dy).

- : subst-cof X ([dx] cof/absd (E dx)) (cof/absd E')
     <- subst-eof X E E'.

- : subst-cof X ([dx] cof/app (E1 dx) (E2 dx)) (cof/app E1' E2')
     <- subst-eof X E1 E1'
     <- subst-eof X E2 E2'.

- : subst-cof X ([dx] cof/val (E dx)) (cof/val E')
     <- subst-eof X E E'.

- : subst-cof X ([dx] cof/op O (E dx) ([y] [dy] K dx y dy) OinD)
     (cof/op O E' K' OinD)
     <- subst-eof X E E'
     <- {y} {dy} subst-cof X ([dx] K dx y dy) (K' y dy).

- : subst-cof X ([dx] cof/let (C1 dx) ([y] [dy] C2 dx y dy)) (cof/let C1' C2')
     <- subst-scof X C1 C1'
     <- {y} {dy} subst-cof X ([dx] C2 dx y dy) (C2' y dy).

- : subst-cof X
     ([dx] cof/letr ([f : expr] [df : var f _] [y] [dy] C1 dx f df y dy) ([f] [df] C2 dx f df))
     (cof/letr C1' C2')
     <- ({y} {dy} {f : expr} {df : var f _} subst-cof X ([dx] C1 dx y dy f df) (C1' y dy f df))
     <- ({y} {dy} subst-cof X ([dx] C2 dx y dy) (C2' y dy)).

- : subst-cof X ([dx] cof/with (E dx) (C dx)) (cof/with E' C')
     <- subst-eof X E E'
     <- subst-cof X C C'.

- : subst-scof X ([dx] scof/plain (C dx)) (scof/plain C')
     <- subst-cof X C C'.

- : subst-scof X ([dx] scof/forall ([t] C dx t)) (scof/forall C')
     <- {t} subst-scof X ([dx] C dx t) (C' t).

- : subst-opcof _ ([dx] opcof/nil) opcof/nil.

- : subst-opcof X ([dx] opcof/cons O ([y] [dy] [k] [dk] K dx y dy k dk) (OCs dx))
     (opcof/cons O K' OCs')
     <- ({y} {dy} {k} {dk} subst-cof X ([dx] K dx y dy k dk) (K' y dy k dk))
     <- subst-opcof X OCs OCs'.

%worlds (open) (subst-eof _ _ _) (subst-cof _ _ _) (subst-scof _ _ _) (subst-opcof _ _ _).
%total {(E C C' C'')} (subst-eof _ E _) (subst-cof _ C _) (subst-scof _ C' _) (subst-opcof _ C'' _).


% Two frequently used instances of the substitution lemma

subst : ({x} var x A -> cof Sig (C x) B) -> eof Sig E A -> cof Sig (C E) B -> type.
%mode subst +C +X -C'.

- : subst C (X : eof Sig E A) C'
     <- subst-cof X (C E) C'.

%worlds (topen) (subst _ _ _).
%total {} (subst _ _ _).


subst2 : ({x1} var x1 A1 -> {x2} var x2 A2 -> cof Sig (C x1 x2) B)
          -> eof Sig E1 A1 -> eof Sig E2 A2 -> cof Sig (C E1 E2) B -> type.
%mode subst2 +C +X1 +X2 -C'.

- : subst2 C (X1 : eof Sig E1 A1) (X2 : eof Sig E2 A2) C''
     <- ({x1} {dx1 : var x1 A1} subst-cof X2 (C x1 dx1 E2) (C' x1 dx1))
     <- subst-cof X1 (C' E1) C''.

%worlds (topen) (subst2 _ _ _ _).
%total {} (subst2 _ _ _ _).

