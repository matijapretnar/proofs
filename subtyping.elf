<r/rest : {E} {I : instance E} {R : region E} R <r (region/cons I R) -> type. 
%mode <r/rest +E +I +R -RIR.

- : <r/rest _ _ _ <r/empty.

%worlds (vars) (<r/rest _ _ _ _).
%trustme %total {} (<r/rest _ _ _ _).

<r/refl : {R} R <r R -> type.
%mode <r/refl +R -RR.

- : <r/refl region/empty <r/empty.
- : <r/refl (region/cons I R) (<r/cons in-region/here RIR)
     <- <r/rest _ _ R RIR.

%worlds (vars) (<r/refl _ _).
%total {} (<r/refl _ _).

<d/rest : {E} {D} {I : instance E} {O} D <d (dirt/cons I O D) -> type. 
%mode <d/rest +E +D +I +O -DIOD.

- : <d/rest _ _ _ _ <d/empty.

%worlds (vars) (<d/rest _ _ _ _ _).
%trustme %total {} (<d/rest _ _ _ _ _).

<d/refl : {D} D <d D -> type.
%mode <d/refl +D -DD.

- : <d/refl dirt/empty <d/empty.
- : <d/refl (dirt/cons I O D) (<d/cons in-dirt-instance/here DIOD)
     <- <d/rest _ D I O DIOD.

%worlds (vars) (<d/refl _ _).
%total {} (<d/refl _ _).

<t/refl : {A} A <t A -> type.
%mode <t/refl +A -AA.
<dt/refl : {B} B <dt B -> type.
%mode <dt/refl +B -BB.

- : <t/refl nat <t/nat.
- : <t/refl bool <t/bool.
- : <t/refl unit <t/unit.
- : <t/refl empty <t/empty.
- : <t/refl (inst R) (<t/inst RR)
     <- <r/refl R RR.
- : <t/refl (A --> B) (<t/--> AA BB)
     <- <t/refl A AA
     <- <dt/refl B BB.
- : <t/refl (A ==> B) (<t/==> AA BB)
     <- <dt/refl A AA
     <- <dt/refl B BB.
- : <dt/refl (A ! D) (<dt/! AA DD)
     <- <t/refl A AA
     <- <d/refl D DD.

%worlds (vars) (<t/refl _ _) (<dt/refl _ _).
%total {(A B)} (<t/refl A _) (<dt/refl B _).

<d/in-dirt-instance : in-dirt-instance I O D -> D <d D' -> in-dirt-instance I O D' -> type.
%mode <d/in-dirt-instance +IiD +DD' -IiD'.

- : <d/in-dirt-instance in-dirt-instance/here (<d/cons IiD' _) IiD'.
- : <d/in-dirt-instance (in-dirt-instance/there IiD) (<d/cons _ DD') IiD'
     <- <d/in-dirt-instance IiD DD' IiD'.

%worlds (vars) (<d/in-dirt-instance _ _ _).
%total {IiD} (<d/in-dirt-instance IiD _ _).

<d/in-dirt : in-dirt R O D -> D <d D' -> in-dirt R O D' -> type.
%mode <d/in-dirt +RiD +DD' -RiD'.

- : <d/in-dirt in-dirt/empty _ in-dirt/empty.
- : <d/in-dirt (in-dirt/cons IiD RiD) DD' (in-dirt/cons IiD' RiD')
     <- <d/in-dirt-instance IiD DD' IiD'
     <- <d/in-dirt RiD DD' RiD'.

%worlds (vars) (<d/in-dirt _ _ _).
%total {IiR} (<d/in-dirt IiR _ _).

<d/trans : D <d D' -> D' <d D'' -> D <d D'' -> type.
%mode <d/trans +DD' +D'D'' -DD''.

- : <d/trans <d/empty _ <d/empty.
- : <d/trans (<d/cons IOD' DD') D'D'' (<d/cons IOD'' DD'')
     <- <d/trans DD' D'D'' DD''
     <- <d/in-dirt-instance IOD' D'D'' IOD''.

%worlds (vars) (<d/trans _ _ _).
%total {DD'} (<d/trans DD' _ _).

instance-in-region-in-dirt : in-region I R -> in-dirt R O D -> in-dirt-instance I O D -> type.
%mode instance-in-region-in-dirt +IinR +ROinD -IOinD.

- : instance-in-region-in-dirt in-region/here (in-dirt/cons IinD _) IinD.
- : instance-in-region-in-dirt (in-region/there IinR) (in-dirt/cons _ RinD) IinD
     <- instance-in-region-in-dirt IinR RinD IinD.

%worlds (vars) (instance-in-region-in-dirt _ _ _).
%total {IinR} (instance-in-region-in-dirt IinR _ _).


<r/in-dirt : R <r R' -> in-dirt R' O D -> in-dirt R O D -> type.
%mode <r/in-dirt +RR' +R'OinD -ROinD.

- : <r/in-dirt <r/empty _ in-dirt/empty.
- : <r/in-dirt (<r/cons IinR' RR') R'inD (in-dirt/cons IinD RinD)
     <- instance-in-region-in-dirt IinR' R'inD IinD
     <- <r/in-dirt RR' R'inD RinD.

%worlds (vars) (<r/in-dirt _ _ _).
%total {RR'} (<r/in-dirt RR' _ _).


