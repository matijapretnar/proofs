Specification "exp2skel".
Import "common".
Import "proof-exp2skel-lemmas".
Set subgoals off.

% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% % Source-to-target elaboration produces typeable terms.
% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Theorem e2s/of_coer :
  (
    forall Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e Y A1 A2, ctx3 Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e ->
    {Ctx_t |- exp/of_coer Y (exp/val_ty_coer_ty A1 A2)} ->
    exists T, {Ctx_t2e |- exp/skel_val_ty A1 T} /\ {Ctx_t2e |- exp/skel_val_ty A2 T}
  ) /\
  (
    forall Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e Y A1 A2, ctx3 Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e ->
    {Ctx_t |- exp/of_coer Y (exp/comp_ty_coer_ty A1 A2)} ->
    exists T, {Ctx_t2e |- exp/skel_comp_ty A1 T} /\ {Ctx_t2e |- exp/skel_comp_ty A2 T}
  ).
induction on 2 2. intros. split.
intros. case H2.
  % unit_refl
  search.
  % fun
  apply IH to H1 H3. apply IH1 to H1 H4. search.
  % handler
  apply IH1 to H1 H3. apply IH1 to H1 H4. search.
  % all skel
  apply IH to H1 H3. search.
  % all val ty
  apply IH to _ H3. search.
  % all dirt
  apply IH to H1 H3. search.
  % qual_ty
  apply IH to H1 H3. search.
  % backchain
  apply ctx_t_member to H1 H4. case H5. case H3. case H3. case H3.
  apply strengthen_skel_val_ty to H1 H7.
  apply strengthen_skel_val_ty to H1 H8.
  search. case H3. case H3.
intros. case H2.
  % bang
  apply IH to H1 H3. search.
  % backchain
  apply ctx_t_member to H1 H4. case H5. case H3. case H3. case H3. case H3.
  apply strengthen_skel_comp_ty to H1 H7.
  apply strengthen_skel_comp_ty to H1 H8.
  search. case H3.

Split e2s/of_coer as e2s/of_val_coer, e2s/of_comp_coer.

Theorem e2s/of :
  (forall Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e Vt A Ve, ctx3 Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e ->
    {Ctx_t2e |- e2s/val Vt Ve} -> {Ctx_t |- exp/of_val Sig_t Vt A} ->
    exists T, {Ctx_t2e |- exp/skel_val_ty A T} /\ {Ctx_e |- skel/of_val Sig_e Ve T})
  /\
  (forall Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e Ht A D B He, ctx3 Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e ->
    {Ctx_t2e |- e2s/hand Ht He} -> {Ctx_t |- exp/of_hand Sig_t Ht A D B} ->
    exists T1 T2, {Ctx_t2e |- exp/skel_val_ty A T1} /\ {Ctx_t2e |- exp/skel_comp_ty B T2} /\ {Ctx_e |- skel/of_hand Sig_e He T1 T2})
  /\
  (forall Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e Ct B Ce, ctx3 Sig_t Sig_e Ctx_t Ctx_t2e Ctx_e ->
    {Ctx_t2e |- e2s/comp Ct Ce} -> {Ctx_t |- exp/of_comp Sig_t Ct B} ->
    exists T, {Ctx_t2e |- exp/skel_comp_ty B T} /\ {Ctx_e |- skel/of_comp Sig_e Ce T}).
induction on 2 2 2. split.
% VALUES
intros. case H2.
% UNIT
case H3.
  search.
  apply ctx_t_member to H1 H5. case H6. case H4. case H7. case H4. case H4. case H4. case H4.
% FUN
case H3.
  apply IH2 to _ H4 H6.
  apply exp/skel_comp_ty_strengthen to H1 H7.
  search.
  apply ctx_t_member to H1 H7. case H8. case H6. case H9. case H6. case H6. case H6. case H6.
% HANDLER
case H3.
  apply IH1 to _ H4 H5.
  search.
  apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.
% SKELETON LAMBDA
case H3.
  apply IH to _ H4 H5. search.
  apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.
% SKELETON APPLY
case H3.
apply IH to H1 H4 H5. case H6.
  inst H8 with n1 = S. cut H9. search.
  apply ctx_e2s/member to H1 H9. case H10. case H8. case H8. case H11.
  apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.
% VALUE LAMBDA
case H3.
  apply IH to _ H4 H5. search.
  apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.
% VALUE APPLY
case H3.
apply IH to H1 H4 H5. case H7. inst H9 with n1 = X.
apply strengthen_skel_val_ty to H1 H6.
cut H10. search.
  apply ctx_e2s/member to H1 H10. case H11. case H9. case H9. case H12.
  apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.
% DIRT LAMBDA
case H3.
apply IH to H1 H4 H5. search.
  apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.
% DIRT APPLY
case H3.
apply IH to H1 H4 H5. case H6. inst H8 with n1 = X. search.
apply ctx_e2s/member to H1 H9. case H10. case H8. case H8. case H11.
  apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.
% COERCION LAMBDA
case H3.
apply IH to _ H4 H5 with Ctx_e = Ctx_e, Ctx_t2e = Ctx_t2e, Sig_t = Sig_t, Sig_e = Sig_e.
apply good_ctx to H1 H6. search.
search.
apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.
% COERCION APPLY
case H3.
apply IH to H1 H4 H5. case H7. search.
apply ctx_e2s/member to H1 H10. case H11. case H9. case H9. case H12.
  apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.
% VALUE COERCION
case H3. apply IH to H1 H4 H5.
apply e2s/of_val_coer to H1 H6.
apply unique_skel_val_ty to H1 H7 H9.
search.
  apply ctx_t_member to H1 H6. case H7. case H5. case H8. case H5. case H5. case H5. case H5.

% BACKCHAIN
apply ctx_e2s/member to H1 H5. case H6.
  % we had translated variable in the context
  case H4. case H7. case H3.
  apply ctx_t_member to H1 H13. case H14. case H12. case H15.
    apply unique_ctx_of_val to H1 H10 H13. search.
    case H12. case H12. case H12. case H12.
  % we had translated skeleton in the context
  case H4.
% HANDLERS
intros. case H2.
% RETURN CASE
case H3.
  apply IH2 to _ H4 H6.
  apply exp/skel_comp_ty_strengthen to H1 H7.
  case H9.
    search.
    apply ctx_e2s/member to H1 H11. case H12. case H10. case H10.
  apply ctx_t_member to H1 H7. case H8. case H6. case H6. case H6. case H6. case H6.
% OP CASE
case H3.
  apply IH1 to H1 H4 H6.
  apply e2s/of_op to H1 H7.
  inst H8 with n2 = n3.
  apply IH2 to _ H5 H16.
  apply strengthen_is_op to H1 H9.
  apply exp/skel_comp_ty_strengthen to _ H17.
  inst H20 with n3 = n1, n4 = n2.
  apply exp/skel_comp_ty_strengthen to _ H21.
  apply unique_skel_comp_ty to H1 H11 H22.
  search.
  apply ctx_t_member to H1 H7. case H8. case H6. case H6. case H6. case H6. case H6.
% BACKCHAIN
apply ctx_e2s/member to H1 H5. case H6. case H4. case H4.

% COMPUTATIONS
intros. case H2.
% APP
case H3.
  apply IH to H1 H4 H6.
  apply IH to H1 H5 H7.
  case H8.
    apply unique_skel_val_ty to H1 H10 H12. search.
  apply ctx_e2s/member to H1 H13. case H14. case H12. case H12. case H15.
apply ctx_t_member to H1 H7. case H8. case H6. case H6. case H6. case H6. case H6.
% LET
case H3.
  apply IH to H1 H4 H6.
  apply IH2 to _ H5 H7.
  apply exp/skel_comp_ty_strengthen to H1 H10.
  search.
apply ctx_t_member to H1 H7. case H8. case H6. case H6. case H6. case H6. case H6.
% RETURN
case H3.
  apply IH to H1 H4 H5.
  search.
apply ctx_t_member to H1 H6. case H7. case H5. case H5. case H5. case H5. case H5.
% OP
case H3.
  apply IH to H1 H4 H8.
  apply IH2 to _ H6 H9.
  apply e2s/of_op to H1 H7.
  apply unique_skel_val_ty to H1 H12 H17.
  apply unique_skel_val_ty to H1 H5 H18.
  apply exp/skel_comp_ty_strengthen to H1 H14.
  search.
apply ctx_t_member to H1 H8. case H9. case H7. case H7. case H7. case H7. case H7.
% DO
case H3.
  apply IH2 to H1 H4 H6.
  case H8.
  apply IH2 to _ H5 H7 with Ctx_e = (skel/of_val Sig_e n2 T :: Ctx_e), Sig_e = Sig_e.
  apply exp/skel_comp_ty_strengthen to H1 H11.
  search.
  apply ctx_e2s/member to H1 H11. case H12. case H10. case H10.
apply ctx_t_member to H1 H7. case H8. case H6. case H6. case H6. case H6. case H6.
% WITH
case H3.
  apply IH2 to H1 H4 H6.
  apply IH to H1 H5 H7.
  case H10.
  apply unique_skel_comp_ty to H1 H8 H12.
  search.
apply ctx_e2s/member to H1 H13. case H14. case H12. case H12. case H15.
apply ctx_t_member to H1 H7. case H8. case H6. case H6. case H6. case H6. case H6.
% COMPUTATION CAST
case H3.
  apply IH2 to H1 H4 H5.
  apply e2s/of_comp_coer to H1 H6.
  apply unique_skel_comp_ty to H1 H7 H9.
  search.
apply ctx_t_member to H1 H6. case H7. case H5. case H5. case H5. case H5. case H5.
% IN CONTEXT
apply ctx_e2s/member to H1 H5. case H6. case H4. case H4.
